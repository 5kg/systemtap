// context-caller tapset
//
// This file is part of systemtap, and is free software.  You can
// redistribute it and/or modify it under the terms of the GNU General
// Public License (GPL); either version 2, or (at your option) any
// later version.
// <tapsetdescription>
// Provides caller and caller_addr function for context for kernel and user
// space.
// </tapsetdescription>

%{
/* caller_addr() might be user caller, so needs at least uprobes structs. */
#include "linux/uprobes-inc.h"
%}

/**
 *  sfunction callers - return list of PCs at top n levels of kernel stack
 *  @n: number of levels to descend in the stack (not counting the top
 *  level). If n is -1, print the entire stack.
 */
function callers:string (n:long) {
         str = ""
         for (i = 0; i <= n || n == -1; i++) {
             foo = i > 0 ? " " : ""
             try {
                 foo .= sprintf("0x%x", stack(i))
             } catch { /* assume we've hit the end of the stack */
                 if (n == -1) break
                 @unwind_error(n)
             }
             str .= foo
         }
         return str
}

/**
 *  sfunction caller - Return name and address of calling function
 *
 *  Description: This function returns the address and name of the 
 *  calling function. This is equivalent to calling:
 *  sprintf("%s 0x%x", symname(caller_addr(), caller_addr()))
 *  Works only for return probes at this time.
 */
function caller:string() {
	return sprintf("%s 0x%x", symname(caller_addr()), caller_addr());
}

/**
 *  sfunction caller_addr - Return caller address
 *
 *  Description: This function returns the address of the calling function. 
 *  Works only for return probes at this time.
 */
function caller_addr:long () %{ /* pure */
        if (CONTEXT->probe_type == _STP_PROBE_HANDLER_KRETPROBE)
		STAP_RETVALUE = (int64_t)(long)_stp_ret_addr_r(CONTEXT->ips.krp.pi);
#ifdef STAPCONF_UPROBE_GET_PC
	else if (CONTEXT->probe_type == _STP_PROBE_HANDLER_URETPROBE)
		STAP_RETVALUE = (int64_t)(long)_stp_ret_addr_r(CONTEXT->ips.ri);
#endif
        else
		STAP_RETVALUE = 0;
%}
